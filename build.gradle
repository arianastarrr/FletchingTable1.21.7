plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version "0.20.0"
    id 'maven-publish'
}

repositories {
    maven { url = "https://repo.spongepowered.org/maven/" }
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    mavenCentral()
    gradlePluginPortal()
}

version = '1.6'
group = 'io.shcm.shsupercm.fabric'

gradlePlugin {
    // Define the plugin
    plugins {
        fletchingtable {
            id = 'io.shcm.shsupercm.fabric.fletchingtable'
            implementationClass = 'io.shcm.shsupercm.fabric.fletchingtable.FTGradlePlugin'
        }
    }
}
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

dependencies {
    compileOnly group: 'commons-io', name: 'commons-io', version: '2.11.0'
    compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    compileOnly group: 'net.fabricmc', name: 'fabric-loom', version: '1.1-SNAPSHOT'
    compileOnly group: 'net.fabricmc', name: 'mapping-io', version: '0.2.1'
}

task apiJar(type: Jar) {
    archiveFileName = "api.jar"

    from sourceSets.main.output
    from sourceSets.main.java.getSrcDirs()
    include "io/shcm/shsupercm/fabric/fletchingtable/api/**"
}

jar.dependsOn(apiJar)

jar {
    into("META-INF/jars") {
        from apiJar
    }
}

pluginBundle {
    website = 'https://github.com/SHsuperCM/FletchingTable'
    vcsUrl = 'https://github.com/SHsuperCM/FletchingTable'

    description = 'Additions and automations for fabric-loom'

    plugins {
        fletchingtable {
            displayName = "Fletching Table"
            tags = ['fabric', 'fabricmc', 'annotation processor']
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'localPluginRepository'
            url = './local-plugin-repository'
        }
    }
}